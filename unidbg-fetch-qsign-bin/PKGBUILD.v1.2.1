# Maintainer: Henry-ZHR <henry-zhr@qq.com>
pkgname=unidbg-fetch-qsign
_version=1.2.1
pkgver="${_version}"
pkgrel=1
pkgdesc='获取QQSign通过Unidbg'
arch=('any')
url='https://github.com/fuqiuluo/unidbg-fetch-qsign'
license=('GPL3')
depends=('java-runtime>=8' 'sh')
makedepends=('java-environment>=8')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "${pkgname}.sh"
	"${pkgname}.service"
	"${pkgname}@.service")
sha512sums=('47fcd7fde9f8322c00fbe8713d02f329e55cced28e4e8c7fbfcdb78fe549f95bd5229a4252c0ff76003d08dbea56791dcee0fab852498b189279a243436f11b9'
            '58ad99a40219bc692566d51b2124dde6385289905b1f25e462a109730c4515cf4bb9d02f1a66cc867892550a3d00c8346745a6d7509b992303044f0db547a55d'
	    'cb77b1cbd22f2a2d8479d448225e1363829275ec10acbf2c147528fd28ba8858e538fe0bb91eeec6c8a73533d3e824e6f93c33b441d68e921dd1fe712516c103'
	    '9d4da1a0f7503ffae8167c5314ef60518550c06d984bbb5f1f431dcc6cbb5d35b26874f1bef5b5412b0af044c0d7d32201d9e0f43827bfbd72291001f9fa0a11')

build() {
  cd "${pkgname}-${pkgver}"
  ./gradlew --no-daemon assemble
}

package() {
  install -Dm755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  cd "${pkgname}-${pkgver}"
  install -Dm644 "build/libs/${pkgname}-${_version}-all.jar" "${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar"

  install -Dm644 "${srcdir}/${pkgname}.service"  "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm644 "${srcdir}/${pkgname}@.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}@.service"
}
